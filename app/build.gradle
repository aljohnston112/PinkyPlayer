plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'com.google.dagger.hilt.android'
}

android {
    namespace 'io.fourth_finger.pinky_player'
    compileSdk 34

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "io.fourth_finger.pinky_player"
        // May be as high as 28, if needed
        minSdk 28
        targetSdk 34
        versionCode 3
        versionName "0.3.3"

        testInstrumentationRunner "io.fourth_finger.pinky_player.hilt.PinkyRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        testInstrumentationRunnerArguments useTestStorageService: 'true'

    }

    buildTypes {

        debug {
            testCoverageEnabled true
            minifyEnabled false
            debuggable true
        }


        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

    }

    flavorDimensions += "version"

    productFlavors {
        create("full") {}
        create("empty"){
            applicationIdSuffix = ".empty"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        adbOptions {
            timeOutInMs 600000
        }
    }

}

dependencies {

    implementation project(':music_repository')
    implementation project(':probability_map')
    implementation project(':settings_repository')

    implementation "androidx.activity:activity-ktx:1.9.0"
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.concurrent:concurrent-futures-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.13.1'

    // https://github.com/androidx/media/issues/1352
    // Do not upgrade until commit 1a5cf67 is in the release
    // It will be in 1.4.0-alpha02
    implementation "androidx.media3:media3-common:1.2.0"
    implementation "androidx.media3:media3-exoplayer:1.2.0"
    implementation 'androidx.media3:media3-session:1.2.0'

    implementation "androidx.navigation:navigation-fragment-ktx:2.7.7"
    implementation "androidx.navigation:navigation-ui-ktx:2.7.7"

    implementation "androidx.preference:preference-ktx:1.2.1"

    implementation "com.google.dagger:hilt-android:2.51.1"
    ksp "com.google.dagger:hilt-compiler:2.51.1"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0-RC'

    debugImplementation "androidx.fragment:fragment-testing:1.8.0"
    debugImplementation "androidx.test:monitor:1.6.1"

    testImplementation 'junit:junit:4.13.2'

    androidTestUtil 'androidx.test:orchestrator:1.4.2'
    androidTestUtil 'androidx.test.services:test-services:1.4.2'

    androidTestImplementation "androidx.arch.core:core-testing:2.2.0"
    androidTestImplementation "androidx.media3:media3-test-utils:1.3.1"
    androidTestImplementation "androidx.navigation:navigation-testing:2.7.7"
    androidTestImplementation 'androidx.test:core-ktx:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.3.0'

    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.51.1'
    kspAndroidTest 'com.google.dagger:hilt-android-compiler:2.51.1'

    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.1'

}
